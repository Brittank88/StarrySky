plugins {
	id 'fabric-loom' version '0.12+'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version          = project.mod_version
group            = project.maven_group

repositories {
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/" }
	maven { url "https://maven.kyrptonaught.dev" }
	maven { url "https://maven.nucleoid.xyz" }
	maven { url "https://maven.parchmentmc.org" }
}

dependencies {

	// Minecraft
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	// Mappings
	mappings loom.layered() {
		yarn_mappings("net.fabricmc:yarn:${project.yarn_mappings}:v2")
		parchment("org.parchmentmc.data:parchment-1.17.1:2021.09.05@zip")
	}

	// Fabric
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Cloth Config
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") { exclude(group: "net.fabricmc.fabric-api") }

	// ModMenu
	modApi("com.terraformersmc:modmenu:${project.modmenu_version}")

	// Custom Portal API
	modImplementation "net.kyrptonaught:customportalapi:$project.custom_portal_api_version" // https://github.com/kyrptonaught/customportalapi
	include "net.kyrptonaught:customportalapi:$project.custom_portal_api_version" // https://github.com/kyrptonaught/customportalapi

	// Polymer
	modImplementation include("eu.pb4:polymer:${project.polymer_version}")	// https://github.com/Patbox/polymer
}

processResources {
	inputs.property "version", project.version
	filesMatching("fabric.mod.json") { expand "version": project.version }
}

tasks.withType(JavaCompile) {
	// Ensure that the encoding is set to UTF-8, no matter what the system default is.
	// This fixes some edge cases with special characters not displaying correctly.
	// See: http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
	options.encoding   = "UTF-8"
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}